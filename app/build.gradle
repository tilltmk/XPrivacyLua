apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 34
    namespace 'eu.faircode.xlua'

    defaultConfig {
        applicationId "eu.faircode.xlua"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 137
        versionName "1.36"
        archivesBaseName = "XPrivacyLua-v$versionName-$versionCode"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    def appcompat_version = "1.6.1"
    def swiperefresh_version = "1.1.0"
    def recyclerview_version = "1.3.2"
    def constraintlayout_version = "2.1.4"
    def material_version = "1.11.0"
    def glide_version = "4.16.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://mvnrepository.com/artifact/androidx.appcompat/appcompat
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    // https://mvnrepository.com/artifact/androidx.swiperefreshlayout/swiperefreshlayout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefresh_version"

    // https://mvnrepository.com/artifact/androidx.recyclerview/recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // https://mvnrepository.com/artifact/androidx.constraintlayout/constraintlayout
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation "com.google.android.material:material:$material_version"

    // https://bumptech.github.io/glide/
    // https://mvnrepository.com/artifact/com.github.bumptech.glide/glide
    implementation("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    annotationProcessor 'androidx.annotation:annotation:1.7.1'
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // https://github.com/rovo89/XposedBridge/wiki/Using-the-Xposed-Framework-API
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    // Embedded LuaJ library
    // http://www.luaj.org/luaj/3.0/README.html
}
